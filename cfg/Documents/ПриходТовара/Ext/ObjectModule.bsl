////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Создание движений в регистре накопления ТоварныеЗапасы
	Движения.ТоварныеЗапасы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриходТовараТовары.Ссылка.Дата КАК Период,
	|	ПриходТовараТовары.Товар КАК Товар,
	|	ПриходТовараТовары.Ссылка.Склад КАК Склад,
	|	ПриходТовараТовары.Количество * ПриходТовараТовары.ЕдиницаИзмерения.Коэффициент КАК Количество
	|ИЗ
	|	Документ.ПриходТовара.Товары КАК ПриходТовараТовары
	|ГДЕ
	|	ПриходТовараТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПриходТовараТовары.НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		Движение = Движения.ТоварныеЗапасы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);

	КонецЦикла;

	// Создание движения в регистре накопления Взаиморасчеты
	Движения.Взаиморасчеты.Записывать = Истина;
	Движение = Движения.Взаиморасчеты.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Поставщик;
	Движение.Договор = Договор;
	Движение.Валюта = Валюта;

	Если Валюта.Пустая() Тогда

		Движение.Сумма = Товары.Итог("Сумма");

	Иначе

		Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Валюта)).Курс;

		Если Курс = 0 Тогда
			Движение.Сумма = Товары.Итог("Сумма");
		Иначе
			Движение.Сумма = Товары.Итог("Сумма") / Курс;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда

		ЗапросПоКонтрагенту = Новый Запрос("ВЫБРАТЬ
		                                   |	Контрагенты.ЭтоГруппа
		                                   |ИЗ
		                                   |	Справочник.Контрагенты КАК Контрагенты
		                                   |ГДЕ
		                                   |	Контрагенты.Ссылка = &КонтрагентСсылка");
		ЗапросПоКонтрагенту.УстановитьПараметр("КонтрагентСсылка", ДанныеЗаполнения);
		Выборка = ЗапросПоКонтрагенту.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.ЭтоГруппа Тогда
			Возврат;
		КонецЕсли;
		
		Поставщик = ДанныеЗаполнения.Ссылка;
	КонецЕсли;

КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//Удалим из списка проверяемых реквизитов валюту, если по организации не ведется 
	//валютный учет
	Если НЕ ПолучитьФункциональнуюОпцию("ВалютныйУчет", Новый Структура("Организация", Организация)) Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Валюта"));
	КонецЕсли;
	
	// Проверим, чтобы у всех единиц измерения в ТЧ был заполнен коэффициент -
	// для предотвращения нулевого количества в регистре ТоварныеЗапасы.
	ДанныеТабЧасти = Товары.Выгрузить(, "НомерСтроки, ЕдиницаИзмерения");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДанныеТабЧасти", ДанныеТабЧасти);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеТабЧасти.НомерСтроки КАК НомерСтроки,
	|	ДанныеТабЧасти.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ДанныеТабЧасти
	|ИЗ
	|	&ДанныеТабЧасти КАК ДанныеТабЧасти
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТабЧасти.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеТабЧасти.ЕдиницаИзмерения) КАК ЕдиницаИзмерения
	|ИЗ
	|	ДанныеТабЧасти КАК ДанныеТабЧасти
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ДанныеТабЧасти.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|ГДЕ
	|	ЕдиницыИзмерения.Коэффициент = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("В строке %1 указана единица измерения ""%2"" с нулевым коэффициентом",
			Формат(Выборка.НомерСтроки, "ЧГ=0"),
			Выборка.ЕдиницаИзмерения);
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = СтрШаблон("Товары[%1].ЕдиницаИзмерения", Формат(Выборка.НомерСтроки - 1, "ЧГ=0"));
		Сообщение.Сообщить();
		
	КонецЦикла;
	
КонецПроцедуры

